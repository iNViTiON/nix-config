# Strata, a keyboard-first file manager (not in nixpkgs yet). Copied from
# github.com/Th1nkK1D/lk-nix (pkgs/strata, MIT); `maintainers` dropped because that
# maintainer is not in our nixpkgs. Update: bump version, set both hashes to lib.fakeHash,
# build, and paste the hashes from the errors.
{
  lib,
  rustPlatform,
  fetchFromGitHub,
  pkg-config,
  wrapGAppsHook4,
  nix-update-script,
  bubblewrap,
  cairo,
  ffmpeg,
  ffmpegthumbnailer,
  fontconfig,
  gdk-pixbuf,
  glib,
  gst_all_1,
  gtk4,
  gtksourceview5,
  imagemagick,
  libraw,
  pango,
  poppler,
  util-linux,
  xdg-terminal-exec,
}:

let
  # PATH the Bubblewrap preview helpers see; upstream hardcodes /usr/bin
  sandboxPath = lib.makeBinPath [
    imagemagick
    libraw
    ffmpeg
    ffmpegthumbnailer
    # The video preview helper runs `prlimit -- ffmpeg …` by name.
    util-linux
  ];
in
rustPlatform.buildRustPackage (finalAttrs: {
  pname = "strata";
  version = "0.20.1";

  src = fetchFromGitHub {
    owner = "lgse";
    repo = "strata";
    tag = "v${finalAttrs.version}";
    hash = "sha256-uAMpUXgcoqejW6acuMbzCEbO8NNFsqlbQf6XP7hMwWY=";
  };

  cargoHash = "sha256-XA6rV+BRj1XG3LlGBBVMUXjnhR1E5FUoL4oT78XazmA=";

  # The preview sandbox is written for an FHS host: it binds /usr, sets the
  # helper PATH to /usr/bin, and runs /usr/bin/prlimit. Point all of that at
  # the store, and hand gdk-pixbuf its loader cache since bwrap clears the
  # environment. bwrap is looked up in fixed system dirs rather than PATH, so
  # pin it to the store too
  postPatch = ''
    substituteInPlace src/sandbox.rs \
      --replace-fail '"/usr/bin",' '"${sandboxPath}", "--setenv", "GDK_PIXBUF_MODULE_FILE", "${gdk-pixbuf}/lib/gdk-pixbuf-2.0/2.10.0/loaders.cache",' \
      --replace-fail '"/usr",' '"/nix/store",' \
      --replace-fail '.arg("/usr/bin/prlimit")' '.arg("${lib.getExe' util-linux "prlimit"}")' \
      --replace-fail 'crate::trusted_command::resolve("bwrap")' 'Ok::<_, String>(std::path::PathBuf::from("${lib.getExe bubblewrap}"))'

    # Hardware video decoding (VA-API) in the preview sandbox: libva loads its driver from
    # /run/opengl-driver on NixOS, which the sandbox doesn't otherwise see.
    substituteInPlace src/sandbox.rs \
      --replace-fail '"/app",' '"/app", "--ro-bind-try", "/run/opengl-driver", "/run/opengl-driver",'

    # The media and browser preview sandboxes look up bwrap the same way.
    substituteInPlace src/sandbox/media.rs src/sandbox/browser.rs \
      --replace-fail 'crate::trusted_command::resolve("bwrap")' 'Ok::<_, String>(std::path::PathBuf::from("${lib.getExe bubblewrap}"))'
  '';

  env.STRATA_BUILD_COMMIT = finalAttrs.src.rev;

  nativeBuildInputs = [
    pkg-config
    glib
    wrapGAppsHook4
  ];

  buildInputs = [
    cairo
    fontconfig
    gdk-pixbuf
    glib
    gst_all_1.gstreamer
    gst_all_1.gst-plugins-base
    gtk4
    gtksourceview5
    pango
    poppler
  ];

  # The suite drives real GTK widgets and Bubblewrap, neither of which the
  # build sandbox provides
  doCheck = false;

  postInstall = ''
    install -Dm644 data/io.github.lgse.Strata.desktop \
      $out/share/applications/io.github.lgse.Strata.desktop
    install -Dm644 data/icons/scalable/apps/io.github.lgse.Strata.svg \
      $out/share/icons/hicolor/scalable/apps/io.github.lgse.Strata.svg

    install -Dm644 data/io.github.lgse.Strata.FileManager1.service \
      $out/share/dbus-1/services/io.github.lgse.Strata.FileManager1.service
    substituteInPlace $out/share/dbus-1/services/io.github.lgse.Strata.FileManager1.service \
      --replace-fail /usr/bin/strata $out/bin/strata

    # Portal backend for org.freedesktop.impl.portal.FileChooser. Upstream
    # installs this per-user from the app; on NixOS it belongs in the package so
    # xdg.portal.extraPortals can pick it up
    install -Dm644 data/portal/strata.portal \
      $out/share/xdg-desktop-portal/portals/strata.portal
    install -Dm644 data/portal/org.freedesktop.impl.portal.desktop.strata.service.in \
      $out/share/dbus-1/services/org.freedesktop.impl.portal.desktop.strata.service
    substituteInPlace $out/share/dbus-1/services/org.freedesktop.impl.portal.desktop.strata.service \
      --replace-fail @STRATA_EXECUTABLE@ $out/bin/strata

    # Marks the install as package-managed so the in-app updater stops offering
    # to overwrite the read-only store path
    install -Dm644 /dev/stdin $out/share/strata/install-source.toml <<EOF
    manager = "Nix"
    update_command = "nixos-rebuild switch"
    EOF
  '';

  # "Open terminal here" shells out to xdg-terminal-exec; GStreamer plays the
  # helper's raw audio frames
  preFixup = ''
    gappsWrapperArgs+=(
      --prefix PATH : "${lib.makeBinPath [ xdg-terminal-exec ]}"
      --prefix GST_PLUGIN_SYSTEM_PATH_1_0 : "${
        lib.makeSearchPath "lib/gstreamer-1.0" (
          with gst_all_1;
          [
            gstreamer
            gst-plugins-base
            gst-plugins-good
            gst-libav
          ]
        )
      }"
    )
  '';

  passthru.updateScript = nix-update-script { };

  meta = {
    description = "Fast, keyboard-first file manager for modern Linux desktops";
    homepage = "https://github.com/lgse/strata";
    changelog = "https://github.com/lgse/strata/releases/tag/v${finalAttrs.version}";
    license = lib.licenses.mit;
    mainProgram = "strata";
    platforms = lib.platforms.linux;
  };
})
